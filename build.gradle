apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group = GROUP
version = VERSION_NAME

def isReleaseBuild
if (hasProperty("release")) {
    isReleaseBuild = true
}

def sonatypeRepositoryUrl

if (isReleaseBuild) {
    println "RELEASE BUILD $version"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
    println "DEBUG BUILD $version"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
}

def username = project.getProperty("nexusUsername") ?: ""
def password = project.getProperty("nexusPassword") ?: ""

dependencies {
    compile gradleApi()
    compile localGroovy()
}

repositories {
    mavenCentral()
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'gradle-android-git'
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { deployment -> signing.signPom(deployment) }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: username, password: password)
                }

                pom.project {
                    name POM_NAME
                    groupId GROUP
                    description POM_DESCRIPTION
                    url POM_URL
                    inceptionYear '2014'

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENSE_NAME
                            url POM_LICENSE_URL
                            distribution POM_LICENSE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                            url POM_DEVELOPER_URL
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}
